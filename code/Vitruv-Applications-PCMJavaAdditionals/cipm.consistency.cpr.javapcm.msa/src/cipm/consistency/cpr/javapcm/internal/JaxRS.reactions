import org.emftext.language.java.classifiers.Annotation

import "http://www.emftext.org/java" as java 
import "http://palladiosimulator.org/PalladioComponentModel/5.2" as pcm


reactions: JaxRS
in reaction to changes in AdjustedJava
execute actions in PCMExtended


import common

routine checkJaxRSPCMSignatureCreation(java::Method interfaceMethod) {
	match {
		require absence of pcm::OperationInterface corresponding to interfaceMethod.containingConcreteClassifier
		require absence of pcm::OperationSignature corresponding to interfaceMethod
	}
	action {
		call {
			for (annot : interfaceMethod.annotationInstances) {
				val usedAnnotation = annot.annotation as Annotation
				var httpMethodAnnotation = false
				for (annotationOnUsedAnnotation : usedAnnotation.annotationInstances) {
					if (annotationOnUsedAnnotation.annotation.name.equals("HttpMethod")) {
						httpMethodAnnotation = true
					}
				}
				if (httpMethodAnnotation && usedAnnotation.name.equals("Path")) {
					createInterface(interfaceMethod.containingConcreteClassifier)
					createPCMSignature(interfaceMethod)
					return
				}
			}
		}
	}
}

routine checkJaxRSInterfaceCreation(java::ConcreteClassifier javaInterface) {
	match {
		val com = retrieve pcm::RepositoryComponent corresponding to javaInterface
	}
	action {
		call {
			for (annot : javaInterface.annotationInstances) {
				val annotName = annot.annotation.name
				if (annotName.equals("Path") || annotName.equals("ApplicationPath")) {
					createInterface(javaInterface)
					return
				}
			}
		}
	}
}
